.inc "../inc/syscall.inc"
.inc "sysinfo.inc"
.inc "proc.lib"
.inc "terminal.lib"
.inc "hwio.lib"

#default interrupt handlers
.decl func scope() {
INT_SYSCALL:
    ori $at, $zero, 40
    bge $k0, $at, INT_SSC_PROCOP

INT_SSC_NOPROCOP:
    #follows calling convention and uses user space
    addi $sp, $sp, -4
    sw $ra, 0($sp)
    
    #at most 4 instructions for each block
    sll $k0, $k0, 4
    lla $t0, INT_SSC1
    add $k0, $k0, $t0
    jr $k0
INT_SSC1:
    #syscall about io is wrapped by terminal
    #case 0
    .dup 4 .0
    #case 1: gotoxy
    jal TERM_GOTOXY #sll 0, 0, 0
    j INT_SSCNOPROCEND #sll 0, 0, 0
    #case 2: getxy
    jal TERM_GETXY #sll 0, 0, 0
    j INT_SSCNOPROCEND #sll 0, 0, 0
    #case 3: putc
    jal TERM_PUTC
    j INT_SSCNOPROCEND
    #case 4: getc
    jal TERM_GETC #sll 0, 0, 0
    j INT_SSCNOPROCEND #sll 0, 0, 0
    #case 5-39: reserved
    .dup 140 .0
INT_SSCNOPROCEND:
    lw $ra, 0($sp)
    addi $sp, $sp, 4
    j INT_SSCEND
    
INT_SSC_PROCOP:
    cache_regs
    lli $sp, SYS_STACK_INIT

    addi $k0, $k0, -40
    sll $k0, $k0, 4
    lla $t0, INT_SSC2
    add $k0, $k0, $t0
    jr $k0
INT_SSC2:
    #case 40: fork
    jal PROC_FORK
    j INT_PROCOPEND
    #case 41: exec
    jal PROC_EXEC
    j INT_PROCOPEND
    #case 42: wait
    jal PROC_WAIT
    j INT_PROCOPEND
    #case 43: sleep
    jal PROC_SLEEP
    j INT_PROCOPEND
    #case 44:
    .dup 4 .0
    #case 45: exit
    jal PROC_EXIT
    j INT_PROCOPEND
    
INT_PROCOPEND:
    #if k1==1, there is no return
    bne $k1, $zero, INT_SSCLRESTORE
    SETFRMLB REGS_BUF
    sw $v0, 8($fp)
INT_SSCLRESTORE:
    restore_regs

INT_SSCEND:
    eret
}

.decl func scope() {
INT_TIMER:
    cache_regs
    lli $sp, SYS_STACK_INIT

    jal PROC_SLEEP_UPDATE
    jal PROC_SCHEDULE

    restore_regs
    eret
}

.decl func scope() {
INT_KEYBOARD:
    cache_regs
    lli $sp, SYS_STACK_INIT

INT_KBDAVL:
    jal HW_GETC
    blt $v0, $zero, INT_KBDEND
    #send the key to terminal
    or $a0, $zero, $v0
    jal TERM_KEYBOARD
    j INT_KBDAVL
INT_KBDEND:
    restore_regs
    eret
}

.decl func scope(main file) {
INT_INIT:
    #interrupt handler for keyboard
    lla $t0, INT_KEYBOARD
    srl $t0, $t0, 2
    lui $t1, 0x0800
    or $t0, $t0, $t1
    swia $t0, 0x80, 0
    lli $t0, 0x40802000 #mtc0 $zero, $4: enable mmu
    swia $t0, 0x84, 0
    #interrupt handler for syscall
    lla $t0, INT_SYSCALL
    srl $t0, $t0, 2
    lui $t1, 0x0800
    or $t0, $t0, $t1
    swia $t0, 0x180, 0
    lli $t0, 0x40802000 #mtc0 $zero, $4: enable mmu
    swia $t0, 0x184, 0
    #interrupt handler for timer
    lla $t0, INT_TIMER
    srl $t0, $t0, 2
    lui $t1, 0x0800
    or $t0, $t0, $t1
    swia $t0, 0x100, 0
    lli $t0, 0x40802000 #mtc0 $zero, $4: enable mmu
    swia $t0, 0x104, 0
    jr $ra
}
