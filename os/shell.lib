.inc "../lib/string.lib"
.inc "../lib/io.lib"
.inc "filesystem.inc"

.static {
SH_PROMPT:
    .str "[root@localhost]\x23 "
SH_BACKGROUND_OP:
    .str "&"
SH_STR_ECHO:
    .str "echo"
SH_MSG_FILE_NOT_FOUND:
    .str " : file not found\n"
}

#tool string functions
.decl func scope() {
SKIP_BLANK:
    ori $t4, $zero, 32  #blank
    or $v0, $zero, $a0
SKP_BLANK_LP:
    lw $t0, 0($v0)
    andi $t0, $t0, 0xff
    bne $t0, $t4, SKP_BLANKEND
    addi $v0, $v0, CHAR_SIZE
    j SKP_BLANK_LP
    
SKP_BLANKEND:
    jr $ra
}

.decl func scope() {
CHOMP:
    addi $sp, $sp, -8
    sw $ra, 0($sp)
    sw $s0, 4($sp)

    or $s0, $zero, $a0
    jal STRLEN
    sll $t1, $v0, 2     #charsize = 4!!!
    add $t1, $t1, $s0

    ori $t4, $zero, 10  #\n
    addi $t1, $t1, -CHAR_SIZE   #the last char
    lw $t0, 0($t1)
    andi $t0, $t0, 0xff
    bne $t0, $t4, CHOMPEND
    sw $zero, 0($t1)    #remove the trailing \n

CHOMPEND:
    lw $ra, 0($sp)
    lw $s0, 4($sp)
    addi $sp, $sp, 8
    jr $ra
}

.decl func scope() {
#a0 NULL terminated str, a1 buffer, get the first symbol seperated by space
SH_GET_SYMBOL:
    addi $sp, $sp, -8
    sw $ra, 0($sp)
    sw $s0, 4($sp)

    or $s0, $zero, $a1
    
    jal SKIP_BLANK

    ori $t4, $zero, 32
    ori $t5, $zero, 10

SH_GETSYB1:
    lw $t0, 0($v0)
    beq $t0, $zero, SH_GETSYBEND
    beq $t0, $t4, SH_GETSYBEND
    beq $t0, $t5, SH_GETSYBEND
    sw $t0, 0($s0)
    addi $s0, $s0, CHAR_SIZE
    addi $v0, $v0, CHAR_SIZE
    j SH_GETSYB1

SH_GETSYBEND:
    lw $ra, 0($sp)
    lw $s0, 4($sp)
    addi $sp, $sp, 8
    jr $ra
}

#impletation of commands
.decl func scope() {
SH_ECHO:
    addi $sp, $sp, -8
    sw $ra, 0($sp)
    sw $s0, 4($sp)

    jal SKIP_BLANK
    or $s0, $zero, $v0

    or $a0, $zero, $s0
    jal PUTS
    ori $a0, $zero, 10
    jal PUTC
    
    lw $ra, 0($sp)
    lw $s0, 4($sp)
    addi $sp, $sp, 8
    jr $ra
}

.decl func scope() {
SH_EXEC: #a0: pt to fileinfo, a1: args
    addi $sp, $sp, -336
    sw $ra, 0($sp)
    sw $s0, 4($sp)
    sw $s1, 8($sp)
    sw $s2, 12($sp)
    .def LOCAL_SH_EXEC_BUF_OFF / 16 /
    .def LOCAL_SH_EXEC_BUF_SIZE / 320 /
    
    or $s0, $zero, $a0
    or $s1, $zero, $a1

    #deal with args
    or $a0, $zero, $s1
    addi $a1, $sp, LOCAL_SH_EXEC_BUF_OFF
    jal SH_GET_SYMBOL
    addi $a0, $sp, LOCAL_SH_EXEC_BUF_OFF
    lla $a1, SH_BACKGROUND_OP
    ori $a2, $zero, 1
    jal STRNCMP
    or $s2, $zero, $v0 #s2 is non-zero then we need to wait the child
    
SH_EXEC_FORK:
    ori $k0, $zero, SC_FORK
    syscall
    beq $v0, $zero, SH_EXEC_CLD
    beq $s2, $zero, SH_EXECEND #no need to wait the child
SH_EXEC_FATHER:
    #wait for childproc v0 to exit
    or $a0, $zero, $v0
    ori $k0, $zero, SC_WAITPID
    syscall
    
SH_EXECEND:
    lw $ra, 0($sp)
    lw $s0, 4($sp)
    lw $s1, 8($sp)
    addi $sp, $sp, 336
    jr $ra

SH_EXEC_CLD:
    ori $k0, $zero, SC_EXEC
    lw $a0, 4($s0) #starting sector
    lw $a1, 8($s0) #size in sector
    syscall
SH_EXEC_EXECERR:
    .3  #errcode 3
    j SH_EXEC_EXECERR
}

.decl func scope(main file) {
SH_CMD:
    addi $sp, $sp, -344
    sw $ra, 0($sp)
    sw $s0, 4($sp)
    sw $s1, 8($sp)
    .def LOCAL_SH_CMD_FILEINFO_OFF / 12 /
    .def LOCAL_SH_CMD_FILEINFO_SIZE / 12 /
    .def LOCAL_SH_CMD_BUF_OFF / 24 /
    .def LOCAL_SH_CMD_BUF_SIZE / 320 /

    #s1 is the cmd and s0 is the args
    #eliminate trailing \n
    or $s0, $zero, $a0
    jal CHOMP

    or $a0, $zero, $s0
    addi $s1, $sp, LOCAL_SH_CMD_BUF_OFF
    or $a1, $zero, $s1
    jal SH_GET_SYMBOL
    or $s0, $zero, $v0
    
    #an empty string
    lw $t0, 0($s1)
    beq $t0, $zero, SH_NOCMD
    #echo
    lla $a1, SH_STR_ECHO
    ori $a2, $zero, 4
    or $a0, $zero, $s1
    jal STRNCMP
    beq $v0, $zero, SH_CMD_ECHO
SH_CMDNF:
    #command not found, test if is a file
    or $a0, $zero, $s1
    addi $a1, $sp, LOCAL_SH_CMD_FILEINFO_OFF
    ori $k0, $zero, SC_STAT
    syscall
    lw $t0, FILE_TABLE_FILENAME_OFF_PER_ITEM($v0)
    beq $t0, $zero, SH_FILENF
    #execute the file
    or $a0, $zero, $v0
    or $a1, $zero, $s0
    jal SH_EXEC
    j SH_CMDEND
    
SH_FILENF:
    or $a0, $zero, $s1
    jal PUTS
    lla $a0, SH_MSG_FILE_NOT_FOUND
    jal PUTS
    j SH_CMDEND
    
SH_NOCMD:
    #empty string
    j SH_CMDEND

SH_CMD_ECHO:
    or $a0, $zero, $s0
    jal SH_ECHO
    j SH_CMDEND
    
SH_CMDEND:
    lw $s0, 4($sp)
    lw $ra, 0($sp)
    addi $sp, $sp, 332
    jr $ra
}
